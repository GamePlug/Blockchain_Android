apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

// 判断版本是Release or Snapshots
def isReleaseBuild() {
    return !VERSION.contains("-SNAPSHOT") && !VERSION.contains("-LOCAL")
}

// 获取仓库url
def getRepositoryUrl() {
    if (VERSION.contains("-SNAPSHOT")) {
        return SNAPSHOT_URL
    } else if (VERSION.contains("-LOCAL")) {
        return LOCAL_URL
    }
    return RELEASE_URL
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            pom.groupId = GROUP_ID
            pom.artifactId = ARTIFACT_ID
            pom.version = VERSION

            repository(url: getRepositoryUrl()) {
                authentication(userName: USERNAME, password: PASSWORD) // maven授权信息
            }
        }
    }
}

// 进行数字签名
signing {
    // 当 发布版本 & 存在"uploadArchives"任务时，才执行
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

// 上传源代码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    if (project.hasProperty('android')) {
        from project.android.sourceSets.main.java.sourceFiles// 用于Android项目
    } else if (sourceSets.hasProperty('main')) {
        from sourceSets.main.groovy.srcDirs// 用于Gradle项目
    }
}
artifacts {
    archives sourcesJar
}
