apply plugin: 'maven'

// 获取仓库url
def getRepositoryUrl() {
    if (VERSION.contains("-SNAPSHOT")) {
        return SNAPSHOT_URL
    } else if (VERSION.contains("-LOCAL")) {
        return LOCAL_URL
    } else if (VERSION.contains("-MODULE")) {
        return rootProject.ext.file.maven
    }
    return RELEASE_URL
}

// 设置Maven
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = GROUP_ID
            pom.artifactId = ARTIFACT_ID
            pom.version = VERSION
            repository(url: getRepositoryUrl()) {
                authentication(userName: USERNAME, password: PASSWORD)
            }
        }
    }
}

// 上传源代码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    if (project.hasProperty('android')) {
        from project.android.sourceSets.main.java.sourceFiles// 用于Android项目
    } else if (sourceSets.hasProperty('main')) {
        if (sourceSets.main.hasProperty("groovy")) {
            from sourceSets.main.groovy.srcDirs// 用于Groovy项目
        } else if (sourceSets.main.hasProperty("java")) {
            from sourceSets.main.java.srcDirs// 用于Java项目
        }
    }
}
artifacts {
    archives sourcesJar
}
